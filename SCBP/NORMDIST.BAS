10 PRINT "NORMAL DISTRIBUTION"
20 PRINT
30 PRINT "(0=STANDARD, 1=NON-STANDARD)"
40 PRINT "WHICH TYPE OF VARIABLE";
50 INPUT S
60 IF S=0 THEN 120
69 REM - LINES 70-110 RQUEST 'NON-STANDARD' VARIABLE DATA
70 PRINT "MEAN";
80 INPUT M
90 PRINT "STANDARD DEVIATION";
100 INPUT S
110 GOTO 130
120 S=1
130 PRINT
140 PRINT "(TO END PROGRAM ENTER X=99999)"
150 PRINT "X =";
160 INPUT X
169 REM - END PROGRAM?
170 IF X=99999 THEN 290
179 REM - ADJUST FOR NON-STANDARD VARIABLES
180 X=(X-M)/S
189 REM - COMPUTE FREQUENCY (y COORDINATE)
190 R=EXP(-X[2/2)/2.5066282746
200 PRINT "FREQUENCY =";R
210 Z=X
219 REM - APPROXIMATE PROBABILITY (AREA UNDER CURVE)
220 T=1/(1+.33267*ABS(X))
230 T=.5-R*(.4361836*T-.1201676*T[2+.937298*T[3)
239 REM - ADJUST FOR NEGATIVE VARIABLES
240 IF Z>=0 THEN 260
250 T=1-T
260 PRINT "PROBABILITY =";T
270 PRINT
280 GOTO 150
290 END
