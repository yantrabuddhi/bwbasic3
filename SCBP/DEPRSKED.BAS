10 PRINT "TAX DEPRECIATION SCHEDULE"
20 PRINT
29 REM - ENTER INITIAL VALUE AND ROUND OFF TO NEAREST CENT
30 PRINT "PURCHASE PRICE";
40 INPUT V
50 V=INT(V*100+.5)/100
59 REM - ENTER END VALUE AND ROUND OFF TO NEAREST CENT
60 PRINT "SALVAGE VALUE";
70 INPUT S
80 S=INT(S*100+.5)/100
89 REM - COMPUTE AMOUNT TO DEPRECIATE
90 D=V-S
99 REM - ENTER LENGTH OF DEPRECIATION
100 PRINT "LIFE IN YEARS";
110 INPUT Y
119 REM - CHOOSE DEPRECIATION METHOD
120 PRINT "ENTER 1 FOR SUM OF DIGITS, 2 FOR DECLINING BALANCE";
130 INPUT X
140 IF X=2 THEN 450
150 IF X<>1 THEN 120
158 REM - BY SUM OF DIGITS METHOD
159 REM - R1 IS THE CUMULATIVE AMOUNT DEPRECIATED
160 R1=0
169 REM - N IS THE PRINTED LINE COUNTER
170 N=66
180 PRINT
190 PRINT
200 FOR I=1 TO Y
209 REM - TEST FOR FULL PAGE
210 IF N<55 THEN 330
219 REM - FULL PAGE; SPACE TO TOP OF NEXT PAGE AND PRINT HEADINGS
220 FOR I1=N TO 66
230 PRINT
240 NEXT I1
250 N=7
260 PRINT "     SUM OF DIGITS TAX DEPRECIATION"
270 PRINT "               PRICE $";V
280 PRINT "         SALVAGE VALUE $";S
290 PRINT "       NET DEPRECIATED $";V-S
300 PRINT "               LIFE";Y;"YEARS"
310 PRINT 
320 PRINT "YEAR","DEPRECIATION","BALANCE"
329 REM - COMPUTE DEPECIATION AND ROUND OFF TO NEAREST CENT
330 R=2*D*(Y-I+1)/((Y+1)*Y)
340 R=INT(R*100+.5)/100
349 REM - ACCUMULATE DEPRECIATION
350 R1=R1+R
360 B=D-R1
369 REM - TEST FOR COMPLETE DEPRECIATION
370 IF B>=0 THEN 410
380 R1=R1+B
390 R=R+B
400 B=0
410 PRINT I,R,B
420 N=N+1
430 NEXT I
440 GOTO 700
448 REM - BY DECLINING BALANCE METHOD
449 REM - ENTER DECLINING BALANCE PERCENT
450 PRINT "METHOD IN %";
460 INPUT M
469 REM - CONVERT PERCENT TO DECIMAL
470 M=M/100
479 REM - N  COUNTS THE LINES PRINTED ON EACH PAGE
480 N=66
489 REM - R IS THE AMOUNT LEFT TO DEPRECIATE
490 R=D
500 PRINT
510 FOR I=1 TO Y
519 REM - TEST FOR A FULL PRINTED PAGE
520 IF N<55 THEN 650
529 REM - FULL PAGE; SPACE TO TOP OF NEXT PAGE AND PRINT HEADINGS
530 FOR I1=N TO 66
540 PRINT
550 NEXT I1
560 N=8
570 PRINT "     DECLINING BALANCE TAX DEPRECIATION"
580 PRINT "           PRICE $";V
590 PRINT "        SALVAGE VALUE $";S
600 PRINT "      NET DEPRECIATED $";D
610 PRINT "           LIFE";Y;"YEARS"
620 PRINT "           METHOD ";M*100;"%"
630 PRINT 
640 PRINT "YEAR","DEPRECIATION","BALANCE"
649 REM - COMPUTE DEPRECIATION AND ROUND OFF TO THE NEAREST CENT
650 R1=INT((R*M/Y)*100+.5)/100
659 REM - ACCUMULATE REMAINING BALANCE
660 R=R-R1
670 PRINT I,R1,R
680 N=N+1
690 NEXT I
700 PRINT
709 REM - RESTART OR END PROGRAM?
710 PRINT "MORE DATA?(1=YES, 0=NO)";
720 INPUT X
730 IF X=1 THEN 20
740 END
