10 PRINT "ROOTS OF QUADRATIC EQUATIONS"
20 PRINT
29 REM - ENTER COEFFICIENTS A,B,C OF A*x[2 + B*X + C
30 PRINT "COEFFICIENTS A,B,C";
40 INPUT A,B,C
50 S=B[2-4*A*C
60 R=SQR(ABS(S))
69 REM - COMPLEX ROOTS?
70 IF S<0 THEN 100
79 REM - CALCULATE ROOTS, LABEL, PRINT
80 PRINT "ROOTS (REAL):   ";(-B-R)/(2*A);",   ";(-B+R)/(2*A)
90 GOTO 110
100 PRINT "ROOTS (COMPLEX): ";-B/(2*A);" + OR -";R/(2*A);" I"
110 PRINT
119 REM - RESTART OR END PROGRAM?
120 PRINT "MORE DATA (1=YES, 0=NO)";
130 INPUT X
140 IF X=1 THEN 20
150 END
