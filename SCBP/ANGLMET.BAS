10 PRINT "ANGLO TO METRIC"
20 PRINT
29 REM - ESTABLISH VARIABLES FOR 17 CONVERSION FACTORS
30 DIM C(17)
39 REM - LOOP TO ASSIGN CONVERSION FACTORS INTO C( )
40 FOR N=1 TO 17
50 READ C(N)
60 NEXT N
69 REM - DATA TABLE OF SEVENTEEN CONVERSION FACTORS
70 DATA 0.3937,3.281E-2,3.281,1.094,0.6214,.01136,.003785,.2366,2.113
80 DATA 1.057,0.2642,0.02838,2.104,0.3527,2.205,9.842E-4,0.6214,0
89 REM - GET NUMBER OF CONVERSION FROM PROGRAM DESCRIPTION
90 PRINT "(TO END PROGRAM ENTER 0)"
100 PRINT "WHICH CONVERSION DO YOU NEED (1 TO 17)";
110 INPUT N
119 REM - END PROGRAM?
120 IF N=0 THEN 540
129 REM - CONVERSION AVAILABLE?
130 IF N>17 THEN 100
140 PRINT "VALUE TO BE CONVERTED";
150 INPUT I
159 REM - PERFORM CONVERSION USING PROPER CONVERSION FACTOR
160 R=I*C(N)
169 REM - DIERCT PROGRAM TO PROPER CONVERSION UNITS, PRINT RESULTS
170 ON N GOTO 180,200,220,240,260,280,300,320,340,360,380,400,420,440,460,480,500
180 PRINT I;"INCHES =";R;"CENTIMETERS"
190 GOTO 520
200 PRINT I;"FEET =";R;"CENTIMETERS"
210 GOTO 520
220 PRINT I;"FEET =";R;"METERS"
230 GOTO 520
240 PRINT I;"YARDS =";R;"METERS"
250 GOTO 520
260 PRINT I;"MILES =";R;"KILOMETERS"
270 GOTO 520
280 PRINT I;"TSP. =";R;"CUBIC CENTIMETERS"
290 GOTO 520
300 PRINT I;"TBSP. =";R;"CUBIC CENTIMETERS"
310 GOTO 520
320 PRINT I;"CUPS =";R;"LITERS"
330 GOTO 520
340 PRINT I;"PINTS =";R;"LITERS"
350 GOTO 520
360 PRINT I;"QUARTS =";R;"LITERS"
370 GOTO 520
380 PRINT I;"GALLONS =";R;"LITERS"
390 GOTO 520
400 PRINT I;"BUSHELS =";R;"LITERS"
410 GOTO 520
420 PRINT I;"PECKS =";R;"LITERS"
430 GOTO 520
440 PRINT I;"OUNCES =";R;"GRAMS"
450 GOTO 520
460 PRINT I;"POUNDS =";R;"KILOGRAMS"
470 GOTO 520
480 PRINT I;"TONS =";R;"KILOGRAM"
490 GOTO 520
499 REM - CONVERT FROM DEGREES FARENHEIT TO CELSIUS
500 R=(I-32)*5/9
510 PRINT I;"DEGREES FAHRENHEIT =";R;"CELSIUS"
520 PRINT
529 REM - RESTART PROGRAM
530 GOTO 100
540 END
