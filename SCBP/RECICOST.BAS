10 PRINT "RECIPE COST"
20 PRINT
29 REM - STATEMENTS 30 TO 180 REQUEST USER INPUT
30 PRINT "NUMBER OF INGREDIENTS";
40 INPUT N
49 REM - LOOP TO REQUEST DATA FOR EACH INGREDIENT
50 FOR I=1 TO N
60 PRINT "INGREDIENT";I;":"
70 PRINT "  COST FOR BULK UNIT IN STORE";
80 INPUT C
90 PRINT "  NUMBER OF UNITS IN BULK";
100 INPUT U
110 PRINT "  NUMBER OF RECIPE UNITS PER BULK UNIT";
120 INPUT F
130 PRINT "  NUMBER OF RECIPE UNITS CALLED FOR";
140 INPUT R
149 REM - SUM COST OF EACH INGREDIENT PER AMOUNT USED
150 P=P+C/U/F*R
160 NEXT I
170 PRINT "NUMBER OF SERVINGS";
180 INPUT S
190 PRINT
199 REM - ROUND OF COSTS TO NEAREST CENT, PRINT RESULTS
200 PRINT "TOTAL COST FOR 1 RECIPE = $";INT(P*100+.5)/100
210 PRINT "COST PER SERVING = $";INT(P/S*100+.5)/100
220 PRINT
229 REM - CALCULATE ALTERNATIVE PRICE PER SERVING?
230 PRINT "CHANGE NUMBER OF SERVINGS (1=YES,0=NO)";
240 INPUT N
250 IF N=1 THEN 170
260 END
