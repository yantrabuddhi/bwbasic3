5 CLS  
10 PRINT  TAB(10);"DEPRECIATION AND INCOME TAX":PRINT   
30 PRINT "AS A BUSINESS EXPENSE HIGH DEPRECIATION LOWERS PROFITS.  
40 PRINT "HOWEVER, LOWER TAXES ARE A RESULT OF THIS. THUS, HIGHER  
50 PRINT "DEPRECIATION MEANS HIGHER NET CASH FLOW WHICH WE DEFINE AS  
60 PRINT "PROFITS BEFORE DEPRECIATION-INCOME TAXES.  
70 PRINT "IN THIS PROGRAM THE USER INPUTS PROFITS BEFORE DEPRECIATION.  
75 PRINT "THEN HE INPUTS VARIOUS POSSIBLE DEPRECIATIONS. NET CASH FLOW  
77 PRINT "FOR EACH IS THEN COMPUTED. TAX SCHEDULES USED ARE:  
80 PRINT "          17% OF FIRST $25000  
82 PRINT "          20% OF NEXT  $25000  
84 PRINT "          30% OF NEXT $25000  
86 PRINT "          40% OF NEXT $25000  
88 PRINT "          46% OF ANY AMOUNT OVER $100000  
95 PRINT :INPUT "PRESS ENTER TO BEGIN";Z9  
98 DEFDBL  B,D,P,T  
100 CLS  
110 F$(1)="     DEPRECIATION         TAXES        NET CASH FLOW !"  
120 F$(2)=" $$########.##     $$########.##     $$########.##"  
130 PRINT "INPUT PROFITS BEFORE DEPRECIATION";:INPUT  PB  
132 PRINT :PRINT "ENTER DEPRECIATION AMOUNT & <ENTER> BELOW"  
135 PRINT   
140 PRINT F$(1)
145 INPUT " ";D 
146 IF D = -1 THEN END 
147 ' PRINT  CHR$(27),CHR$(27)  
150 P=PB-D  
160 IF  P<0  THEN  T=0:GOTO  200  
170 IF  P<25000  THEN  T=.17*P:GOTO  200  
180 IF  P<50000  THEN  T=4250+.20*(P-25000):GOTO  200  
183 IF  P<75000  THEN  T=9250+.30*(P-50000):GOTO  200  
185 IF  P<100000  THEN  T=16750+.40*(P-75000):GOTO  200  
190 T=26750+.46*(P-100000)  
200 PRINT USING  F$(2);D,T,PB-T  
210 GOTO  145  

