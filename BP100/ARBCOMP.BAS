1 REM ARBCOMP  
5 CLEAR  1000  
10 CLS  
20 PRINT  TAB(10); "ARBITRAGE COMPUTATIONS":PRINT   
25 S=0:REM TOTAL VALUE OF PACKAGE  
30 PRINT "ARBITRAGE IS SIMPLY BUYING AN ARTICLE IN ONE MARKET AND  
35 PRINT "SELLING IT IN ANOTHER MARKET, HOPEFULLY AT A HIGHER PRICE.  
40 PRINT "IN THE STOCK MARKET THIS MAY INVOLVE BUYING ONE SECURITY  
45 PRINT "AND SELLING ANOTHER EQUIVALENT SECURITY IN THE SAME MARKET.  
50 PRINT "THIS TYPE OF ARBITRAGE OCCURS WHEN MERGERS ARE BEING CONSID-  
55 PRINT "ERED. OWNERS OF ONE SECURITY MAY TRADE THEM IN FOR ANOTHER  
60 PRINT "PACKAGE OF SECURITIES. IN THIS CASE THE OWNERS OF THE FIRST  
65 PRINT "SECURITY MAY WONDER WHAT THE VALUE OF THE SECOND PACKAGE IS.  
70 PRINT "THIS PROGRAM EVALUATES THE VALUE OF A PACKAGE OF DIFFERENT  
75 PRINT "SECURITIES. IN A MERGER A PACKAGE OF COMPANY A SECURITIES AND  
80 PRINT "CASH ARE GENERALLY TRADED FOR A PACKAGE OF COMPANY B SECURITIES  
85 PRINT "USUALLY SOME SHARES OF COMMON STOCK.  
90 PRINT :INPUT "PRESS ENTER TO BEGIN";Z9$  
100 DIM  A(100),B(100),C(100)  
110 REM A( )=VALUE OF 1 UNIT:B( )=# UNITS:C( )=EXTENDED VALUE  
200 CLS  
210 PRINT "FOR EACH SECURITY IN PACKAGE ENTER PRICE/UNIT AND NO. UNITS  
220 PRINT "IN PACKAGE.  
230 I=1  
240 PRINT "ENTER VALUE OF 1 UNIT OF SECURITY";I;"(-1 TO END)";:INPUT  A(I)  
250 IF  A(I)=-1  THEN  GOTO  300  
260 PRINT "ENTER # OF UNITS OF SECURITY";I;"IN PACKAGE";:INPUT  B(I)  
270 C(I)=A(I)*B(I):S=S+C(I)  
280 I=I+1:PRINT :GOTO  240  
300 REM FINISH UP & PRINT  
310 I=I-1  
320 F$(1)="VALUE OF UNIT       # OF UNITS        TOTAL VALUE"  
330 F$(2)=" $$#####.##         #####.####        $$######.###"  
340 F$(3)="             GRAND TOTAL VALUE       $$#######.###"  
350 CLS  
360 INPUT "IS LINE PRINTER OUTPUT DESIRED(Y/N)";Z2$  
370 IF  Z2$<>"Y"  AND  Z2$<>"N"  THEN  GOTO  360  
375 PRINT :IF  Z2$="Y"  THEN  INPUT "PRESS ENTER WHEN PRINTER READY";Z9$  
380 CLS  
400 PRINT  F$(1):PRINT :IF  Z2$="Y"  THEN  LPRINT  F$(1):LPRINT " "  
410 FOR  J=1  TO  I  
420 PRINT USING  F$(2);A(J),B(J),C(J)  
430 IF  Z2$="Y"  THEN  LPRINT  USING  F$(2);A(J),B(J),C(J)  
440 NEXT  J  
450 PRINT  STRING$(64,"="):IF  Z2$="Y"  THEN  LPRINT  STRING$(64,"=")  
460 PRINT USING  F$(3);S:IF  Z2$="Y"  THEN  LPRINT  USING  F$(3);S  
470 PRINT   
480 INPUT "PRESS ENTER FOR MORE COMPUTATION";Z9$  
485 IF Z9$ = "0" THEN END
490 GOTO  200  

