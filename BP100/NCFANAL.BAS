1 REM NCFANAL  
5 CLEAR  1000  
10 CLS:PRINT  TAB(5); "NET CASH FLOW ANALYSIS":PRINT   
20 PRINT "WHEN INVESTING IN A NEW PROJECT A FIRM MUST TAKE INTO   
25 PRINT "ACCOUNT THE CASH FLOWS GENERATED BY THE PROJECT, BOTH POSITIVE   
30 PRINT "AND NEGATIVE. IN THIS PROGRAM WE ASSUME THAT AN ITEM IS  
35 PRINT "PURCHASED FOR A FIXED AMOUNT, TO BE DEPRECIATED OVER ITS  
40 PRINT "USEFUL LIFE VIA THE STRAIGHT-LINE METHOD. WE ESTIMATE CASH  
45 PRINT "INFLOWS AND OUTFLOWS OVER THE LIFE OF THE PROJECT. THE  
50 PRINT "PROGRAM WILL THEN PRINT A CASH FLOW TABLE"  
90 INPUT "PRESS ENTER TO BEGIN";Z9$  
95 DIM  CI(100),CO(100)  
100 CLS  
110 INPUT "INPUT CASH COST OF THE PROJECT          ";CP  
115 INPUT "INPUT DEPRECIABLE COST OF THE PROJECT   ";CD  
120 INPUT "INPUT USEFUL LIFE OF PROJECT IN YEARS   ";UL  
130 INPUT "SALVAGE VALUE AT END OF USEFUL LIFE     ";SV  
135 AD=(CD-SV)/UL:REM ANNUAL DEPRECIATION  
140 INPUT "INPUT CORPORATE INCOME TAX RATE(%)      ";TX:TX=TX/100  
200 CLS  
210 FOR  I=1  TO  UL  
220 PRINT "CASH FLOWS FOR YEAR  ";I;" NOT INCLUDING INCOME TAXES"  
230 INPUT "        CASH INFLOWS  = ";CI(I)  
240 INPUT "        CASH OUTFLOWS = ";CO(I)  
250 PRINT   
260 NEXT  I  
300 REM PRINTOUT AND COMPUTATUION  
310 F$(1)="YR   CASH    CASH      DEPREC-     NET    TAXES    NET CASH"  
320 F$(2)="    INFLOW  OUTFLOW     IATION    INCOME              FLOW  "  
330 F$(3)="##  ####### #######    #######   #######  ########  #######"  
350 INPUT "DO YOU WANT LINE PRINTER OUTPUT(Y/N)";Z2$  
360 IF  Z2$<>"Y"  AND  Z2$<>"N"  THEN  GOSUB  10000:GOTO  350  
400 CLS  
410 PRINT  F$(1):PRINT  F$(2):IF  Z2$="Y"  THEN  LPRINT  F$(1):LPRINT  F$(2):LPRINT " "  
420 PRINT USING  F$(3);0,0,CP,0,0,0,-CP  
425 IF  Z2$="Y"  THEN  LPRINT  USING  F$(3);0,0,CP,0,0,0,-CP  
430 FOR  I=1  TO  UL  
435 NI=CI(I)-CO(I)-AD:NCF=NI+AD-NI*TX  
437 IF  I=UL  THEN  NCF=NCF+SV  
440 PRINT USING  F$(3);I,CI(I),CO(I),AD,NI,NI*TX,NCF  
450 IF  Z2$="Y"  THEN  LPRINT  USING  F$(3);I,CI(I),CO(I),AD,NI,NI*TX,NCF  
460 IF  I/12=INT(I/12)  THEN  PRINT :GOSUB  10100:CLS:PRINT  F$(1):PRINT  F$(2)  
470 NEXT  I  
480 PRINT :PRINT "YEAR";UL;"CASH FLOW INCLUDES SALVAGE VALUE SALE OF $";SV  
490 IF  Z2$="Y"  THEN  LPRINT " ":LPRINT "YEAR";UL;"CASH FLOW INCLUDES SALVAGE VALUE SALE OF $";SV  
500 PRINT :INPUT "PRESS ENTER FOR MORE ";Z9$  
505 IF Z9$ = "-1" THEN END
510 GOTO  100  
10000 PRINT "INPUT ERROR! TRY AGAIN!":FOR  JJ=1 TO 300:NEXT :RETURN   
10100 INPUT "PRESS ENTER TO CONTINUE";Z9$:RETURN   
