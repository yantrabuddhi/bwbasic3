5 CLS  
90 PRINT "                   OPTION WRITING"  
100 PRINT "tHIS PROGRAM COMPUTES SOME USEFUL TOTALS FOR POTENTIAL  
110 PRINT "OPTION WRITERS. tHE USER OF THIS PROGRAM WILL GENERALLY  
120 PRINT "BE A RATIO WRITER AND WILL SELL A FIXED NUMBER OF OPTIONS  
130 PRINT "AS WELL AS BUY OR SHORT A GIVEN NUMBER OF SHARES OF THE   
140 PRINT "UNDERLYING STOCK. uSING THIS PROGRAM HE WILL BE ABLE TO  
150 PRINT "COMPUTE THE MAXIMUM DOLLAR PROFIT HE MAY REALIZE, THE  
160 PRINT "DOLLAR INVESTMENT REQUIRED FOR THE DEAL, THE MAXIMUM  
170 PRINT "RETURN ON INVESTMENT, AND THE UPPER AND LOWER BREAK  
180 PRINT "EVEN POINTS OF STOCK PRICE FOR THE DEAL.  
190 PRINT :INPUT "PRESS ENTER TO BEGIN ";Z9  
200 CLS  
300 REM INPUT SECTION  
310 INPUT "INPUT C IF OPTION IS CALL, P IF PUT   ";TP$  
315 IF  TP$<>"C"  AND  TP$<>"P" THEN PRINT "INPUT ERROR!":GOTO  310  
320 INPUT "ENTER NUMBER OF OPTION CONTRACTS SOLD      ";N  
330 INPUT "ENTER PRICE PER OPTION                     ";PO  
340 INPUT "ENTER TOTAL COMMISSION PAID ON OPTIONS     ";CO  
350 INPUT "EXERCISE PRICE                             ";EX  
360 INPUT "# OF SHARES BOUGHT OR SHORTED              ";NS  
365 INPUT "PRICE PER SHARE OF STOCK                   ";P  
370 INPUT "AV. COMM. ON ST. SHARE AS % OF SHARE PRICE ";CA  
380 INPUT "DIVIDENDS PER SHARE PAID BY STOCK          ";D  
390 INPUT "MAINTENANCE REQUIREMENT                    ";MN  
400 IF  TP$="P" THEN GOTO  500  
405 REM CALL OPTION CALCULATION  
410 MAXP = N*100*PO-CO+NS*(EX*(1-CA/100)-P*(1+CA/100)+D)  
420 INV = NS*P*(1+CA/100)-(N*100*PO-CO)+MN  
425 RMAX = MAXP/INV*100  
430 U=(MAXP+(N*100-NS)*EX*(1-CA/100))/((100*N-NS)*(1+CA/100))  
440 L=(MAXP-NS*EX*(1-CA/100))/(-NS*(1-CA/100))  
450 GOTO  600  
500 REM PUT OPTION CALCULATION  
510 MAXP = N*100*PO-CO+NS*(P*(1-CA/100)-EX*(1+CA/100)-D)  
520 INV = NS*P/2+NS*P*CA/100-(N*100*PO-CO)+MN  
525 RMAX=MAXP/INV*100  
530 U=(MAXP + NS*EX*(1+CA/100))/(NS*(1+CA/100))  
540 L=(MAXP-(N*100-NS)*EX*(1+CA/100))/(-(N*100-NS)*(1-CA/100))  
550 GOTO  600  
600 REM PRINTOUT  
610 CLS  
620 PRINT USING "MAXIMUM PROFIT                  = $$#####.##";MAXP  
630 PRINT USING "INITIAL INVESTMENT              = $$#####.##";INV  
640 PRINT USING "MAXIMUM RETURN                  =   #####.## PER CENT";RMAX  
650 PRINT USING "UPPER BREAK EVEN POINT          =     ###.## PER SHARE";U  
660 PRINT USING "LOWER BREAK EVEN POINT          =     ###.## PER SHRE";L  
670 PRINT :INPUT "PRESS ENTER TO CONTINUE";Z9  
675 IF Z9 = -1 THEN END
680 CLS:GOTO  310  

