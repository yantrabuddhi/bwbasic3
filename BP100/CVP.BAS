1 REM CVP
5 CLEAR 1000
10 CLS
20 PRINT TAB(10); "COST-VOLUME-PROFIT ANALYSIS":PRINT
30 PRINT"THE BUSINESSMAN IS VERY INTERESTED IN INCOME AFTER TAXES.
35 PRINT"SOMETIMES IT IS VERY DIFFICULT TO FIND THIS. VARIABLES WHICH
40 PRINT"MUST BE CONSIDERED INCLUDE  VARIABLE COSTS PER UNIT,
45 PRINT"FIXED COSTS, TAX RATE,SALES DOLLARS, SALES PRICE PER UNIT.
50 PRINT"THE CURRENT PROGRAM ALSO USES RATIO OF VARIABLE COSTS PER UNIT 
55 PRINT"TO SALES PRICE PER UNIT.
60 PRINT"WITH THE ABOVE THIS PROGRAM COMPUTES INCOME AFTER TAXES.
65 PRINT"AS AN OPTION IT CAN ALSO COMPUTE SALES DOLLARS NEEDED FOR
70 PRINT"A SPECIFIED INCOME.
90 PRINT
95 INPUT"PRESS ENTER TO BEGIN";Z9$
100 CLS
110 PRINT TAB(10); "MENU":PRINT
115 PRINT"0. QUIT
120 PRINT"1. DETERMINE SALES FOR SPECIFIED INCOME
130 PRINT"2. DETERMINE INCOME FOR SPECIFIED SALES
150 PRINT:INPUT"ENTER YOUR CHOICE";Z9
155 IF Z9 = 0 THEN END
160 ON Z9 GOSUB 200,300
170 GOTO 100
200 REM FIND SALES FOR SPECIFIED INCOME
210 CLS:GOSUB 500
220 INPUT"ENTER DESIRED INCOME LEVEL           ";I
230 S = (1/(1-R))*(FC + I/(1-TX))
240 F$(1)="TO REACH INCOME LEVEL $$######.## YOU MUST SELL $$#######.##    IN GOODS"
250 PRINT USING F$(1);I,S
290 PRINT:INPUT"PRESS ENTER TO CONTINUE";Z9$
295 RETURN
300 REM DETERMINE INCOME FOR SPECIFIED SALES
310 CLS:GOSUB 500
320 INPUT"ENTER SPECIFIED SALES LEVEL           ";S
330 I = (1-TX)*(S*(1-R)-FC)
340 F$(1)="FOR SALES LEVEL $$#######.## YOUR INCOME WILL EQUAL $$#####.##"
350 PRINT USING F$(1);S,I
390 PRINT:INPUT"PRESS ENTER TO CONTINUE";Z9$
395 RETURN
500 REM INPUT SUBROUTINE
510 INPUT"ENTER TOTAL FIXED COSTS IN DOLLARS    ";FC
520 INPUT"ENTER TAX RATE AS PER CENT            ";TX:TX=TX/100
530 INPUT"ENTER RATIO OF VARIABLE COSTS PER UNIT                               TO SALES PRICE PER UNIT          ";R
590 RETURN
