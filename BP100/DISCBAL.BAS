1 REM DISCBAL
5 CLEAR 1000
10 CLS
20 PRINT TAB(5); "TRUE INTEREST RATE ON DISCOUNTED LOAN":PRINT
30 PRINT"MOST CONSUMER BANK LOANS ARE WHAT ARE CALLED 'DISCOUNT LOANS'.
35 PRINT"IN A LOAN OF THIS TYPE THE FINANCE CHARGE IS PREPAID.
40 PRINT"FOR EXAMPLE, THE BANK WILL GIVE YOU A LOAN WITH FACE VALUE
45 PRINT"EQUAL TO A STATED AMOUNT(TOTAL OF REPAYMENTS) AND WILL THEN
50 PRINT"DEDUCT THE INTEREST CHARGES FROM THE PROCEEDS OF THE LOAN.
55 PRINT"THUS THE CONSUMER DOES NOT GET THE FACE VALUE, BUT SOME LOWER
57 PRINT"AMOUNT. TO MAKE MATTERS WORSE CHARGES FOR ACCIDENT & HEALTH
58 PRINT"INSURANCE,CREDIT LIFE INSURANCE, AND OFFICIAL FEES MAY BE
60 PRINT"REQUIRED.THIS PROGRAM COMPUTES THE PROCEEDS AND THE EFFECTIVE
65 PRINT"INTEREST RATE IN SUCH A CASE."
70 PRINT"THE CHARGES SUCH AS A & H ARE CONSIDERED AND TEND TO RAISE
75 PRINT"THE EFFECTIVE RATE.
90 PRINT:INPUT"PRESS ENTER TO BEGIN";Z9$
100 CLS
110 INPUT"TOTAL REPAYMENTS OF LOAN       ";FV
120 INPUT"TOTAL INTEREST PREPAID         ";IR
122 INPUT"TOTAL A & H INSURANCE CHARGES  ";IH
124 INPUT"TOTAL CREDIT LIFE INS. CHARGES ";IC
126 INPUT"TOTAL OFFICIAL FEES CHARGES    ";IO
130 PV=FV-IR-IH-IC-IO:REM PROCEEDS OF LOAN
140 INPUT"MONTHLY LOAN PAYMENT          ";PMT
150 INPUT"TOTAL NUMBER OF MONTHLY PMTS  ";N
160  GOSUB 4200:REM COMPUTE EFFECTIVE INTEREST RATE
170  F$(1)="%                          %   $$######.##"
180  F$(2)="%                          %        ###.##"
190 PRINT
200 PRINT USING F$(1);"PROCEEDS OF LOAN",PV
205 I=1200*I
210 PRINT USING F$(2);"ANNUAL EFFECTIVE INT. RATE",I;:PRINT"%"
220 PRINT:INPUT"PRESS ENTER FOR MORE COMPUTATIONS";Z9$
225 IF Z9$ = "-1" THEN END
230 GOTO 100
4200 REM I FOR DISCOUNT LOAN
4210 I=.01
4220 P=PMT/I*(1-(1+I)[(-N))-PV
4225 D=N*PMT/(I*(1+I)[(N+1))-PMT/(I[2)*(1-(1+I)[(-N))
4230 GOSUB 4600
4240 IF ABS(P)<=.1 THEN RETURN
4250 GOTO 4220
4600 REM COMPUTATION
4610 I=I-P/D
4620 RETURN
