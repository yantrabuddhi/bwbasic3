5 CLS
990 PRINT"               LEASE INTEREST RATE"
1000 PRINT"THERE ARE MANY TYPES OF EQUIPMENT LEASES. ONE TYPE OF AUTO
1010 PRINT"LEASE IS CALLED AN OPEN END LEASE. THE WAY IT WORKS IS THAT
1020 PRINT"THE LESSEE PAYS THE LEASING COMPANY A DOWN PAYMENT AND A FIXED
1030 PRINT"AMOUNT EACH MONTH FOR A GIVEN NUMBER OF MONTHS. AT THE END
1040 PRINT"OF THE LEASE PERIOD THE LESSEE MUST PAY THE LEASING COMPANY A
1050 PRINT"PRE-AGREED AMOUNT (BUY-BACK AMOUNT). THE LESSEE THEN OWNS THE
1060 PRINT"CAR. THERE ARE SEVERAL ADVANTAGES TO LEASING. IF THE INITIAL
1070 PRINT"DOWN PAYMENT IS LOW AND PAYMENTS ARE LESS PER MONTH THAN ON A
1080 PRINT"STRAIGHT LOAN THEN THE LESSEE PAYS LESS MONEY EARLY THAN
1090 PRINT"ON THE LOAN. THE BALLOON PAYMENT MAKES UP FOR THIS LATER.
1100 PRINT"THE PRESENT PROGRAM COMPUTES AN EFFECTIVE INTEREST RATE ON THE
1110 PRINT"LEASE GIVEN THE PURCHASE PRICE OF THE CAR, THE DOWN PAYMENT
1120 PRINT"THE MONTHLY PAYMENT, AND THE BUY-BACK AMOUNT.
1130 PRINT:INPUT"PRESS ENTER TO BEGIN ";Z9$
1200 CLS
1202 PRINT"IT IS ASSUMED THAT THE BUY-BACK OCCURS ONE MONTH AFTER THE
1204 PRINT"LAST PAYMENT. IF THE LAST PAYMENT AND THE BUY-BACK OCCUR AT
1206 PRINT"THE SAME TIME THEN DECREASE THE NUMBER OF PAYMENTS BY 1 AND
1207 PRINT"INCREASE THE BUY-BACK BY THE AMOUNT OF 1 PAYMENT
1208 PRINT:PRINT"   IN ADDITION, THE FIRST PAYMENT MAY BE AT THE END OF THE
1210 PRINT"1ST MONTH, 2ND MONTH, ETC. 
1211 CLEAR 300:DIM A(400)
1213 PRINT
1215 INPUT"INPUT CASH COST OF ITEM          ";CASH
1220 INPUT"INPUT DOWN PAYMENT               ";A(0)
1230 INPUT"INPUT MONTHLY PAYMENT            ";PAYMENT
1240 INPUT"INPUT NUMBER OF MONTHLY PAYMENTS ";N
1250 INPUT"INPUT MONTH OF 1ST PAYMENT       ";FIRST
1260 INPUT"INPUT BUY-BACK AMOUNT            ";BUYBACK
1300 CLS: PRINT:PRINT:PRINT
1310 PRINT"COMPUTING INTEREST RATE "
1320 R=0:JUMP=.1
1330 P=0
1335 PRINT @ 1,"PASS"; LZ;"    ";"CURRENT MONTHLY RATE =";100*R;"%":LZ=LZ+1
1340 P=-CASH
1350 P=P+A(0)
1360 FOR K=FIRST TO FIRST+N-1:P=P+PAYMENT/(1+R)[K:NEXT K
1370 P=P+BUYBACK/(1+R)[(FIRST+N)
1380 IF ABS(P)<.5 THEN GOTO 1500
1390 IF P>0 THEN R=R+JUMP:GOTO 1330
1400 R=R-JUMP:JUMP=JUMP/10:R=R+JUMP:GOTO 1330
1500 REM PRINTOUT ROUTINE
1510 CLS
1515 PRINT"MONTHLY INTEREST RATE      =  ";100*R;"%"
1520 RA=(1+R)[12-1
1530 PRINT"ANNUAL INTEREST RATE(EFF)  = ";100*RA;"%"
1540 PRINT"ANNUAL INTEREST RATE(SIMP) = ";100*R*12;"%"
1545 PRINT:PRINT TAB(10); "INTEREST RATE(SIMP) COMPUTED BY MULTIPLYING":PRINT TAB(10); "MONTHLY INTEREST RATE BY 12 WHILE INTEREST":PRINT TAB(10); "RATE(SIMP) IS A COMPOUNDED MONTHLY RATE"
1550 PRINT:INPUT"PRESS ENTER FOR MORE COMPUTATIONS";Z9$
1555 IF Z9$ = "-1" THEN END
1560 CLS:GOTO 1211
