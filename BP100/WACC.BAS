1 REM WACC  
5 CLEAR  1000  
10 CLS  
20 PRINT  TAB(29); "WACC":PRINT   
30 PRINT "A FIRM MAY BE FINANCED IN SEVERAL DIFFERENT WAYS. AMONG THESE  
35 PRINT "ARE COMMON STOCKS, PREFERRED STOCKS, DEBT, AND RETAINED   
40 PRINT "EARNINGS. GIVEN THE AMOUNT OF EACH OF THE ABOVE AND ITS  
45 PRINT "COST IN PER CENT THIS PROGRAM WILL FIND OUT WHAT PRICE THE  
50 PRINT "FIRM IS PAYING FOR ITS MONEY ON AVERAGE. THIS AVERAGE IS  
55 PRINT "CALLED THE WEIGHTED AVERAGE COST OF CAPITAL (WACC).  
90 PRINT :INPUT "PRESS ENTER TO BEGIN";Z9$  
100 CLS  
110 INPUT "INPUT CURRENT AMOUNT OF DEBT            ";DEBT  
120 INPUT "INPUT CAPITAL DEVOTED TO PREFERRED STOCK";PX  
130 INPUT "INPUT COMMON STOCK CAPITAL              ";C0  
140 INPUT "INPUT RETAINED EARNINGS                 ";RETAINED  
150 PRINT   
160 PRINT "THE FOLLOWING INPUTS ARE AFTER TAX COST":PRINT   
170 INPUT "INPUT COST OF DEBT (%)                 ";DC:DC=DC/100  
180 INPUT "INPUT COST OF PREFERRED STOCK (%)      ";PC:PC=PC/100  
190 INPUT "INPUT COST OF COMMON STOCK (%)         ";CC:CC=CC/100  
200 INPUT "INPUT COST OF RETAINED EARNINGS (%)    ";CR:CR=CR/100  
220 T0=DE+PX+C0+RE  
225 REM PERCENTAGES OF TOTAL FOR DIFFERENT SOURCES  
230 A(1)=100*DE/T0:A(2)=100*PX/T0  
240 A(3)=100*C0/T0:A(4)=100*RE/T0  
245 REM WEIGHTED COSTS BELOW  
250 B(1)=A(1)*DC:B(2)=A(2)*PC:B(3)=A(3)*CC:B(4)=A(4)*CR  
300 F$(1)="METHOD OF FINANCING    PROPORTION   COST     WEIGHTED COST"  
305 F$(4)="                         (%)        (%)          (%)"  
310 F$(2)="%                  %     ###.#      ###.#        ###.##"  
315 F$(3)="WEIGHTED AVERAGE COST OF CAPITAL                 ###.##"  
400 CLS:PRINT  F$(1):PRINT  F$(4):PRINT   
410 PRINT USING  F$(2);"DEBT",A(1),100*DC,A(1)*DC  
420 PRINT USING  F$(2);"PREFERRED STOCK",A(2),100*PC,A(2)*PC  
430 PRINT USING  F$(2);"COMMON STOCK",A(3),100*CC,A(3)*CC  
440 PRINT USING  F$(2);"RETAINED EARNINGS",A(4),100*CR,A(4)*CR  
450 WACC= A(1)*DC+A(2)*PC+A(3)*CC+A(4)*CR  
460 PRINT USING  F$(3);WACC  
500 PRINT :INPUT "PRESS ENTER FOR MORE COMPUTATION";Z9$  
505 IF Z9$ = "-1" THEN END
510 GOTO  100  

